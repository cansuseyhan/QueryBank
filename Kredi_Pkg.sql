/*
KrediID: Kredi kaydının benzersiz kimliği
MusteriID: Krediyi alan müşteri
AnaPara: Kredi ana tutarı
KalanBorc: Kalan borç miktarı
FaizOrani: Uygulanan faiz oranı
VadeTarihi: Kredi vade bitiş tarihi
Durum: Kredi durumu (AKTIF, KAPANDI)
BasvuruTarihi: Kredi başvuru tarihi
*/

CREATE TABLE Kredi (
    KrediID       NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,  
    MusteriID     NUMBER       NOT NULL,                                
    AnaPara       NUMBER       NOT NULL,                                
    KalanBorc     NUMBER       NOT NULL,                                
    FaizOrani     NUMBER(5,2)  NOT NULL,                                
    VadeTarihi    DATE         NOT NULL,                                
    Durum         VARCHAR2(20) DEFAULT 'AKTIF',                         
    BasvuruTarihi DATE         DEFAULT SYSDATE,                         
    
    CONSTRAINT fk_kredi_musteri
        FOREIGN KEY (MusteriID) REFERENCES Musteri(MusteriID)
);

/*
KrediBasvuru: Yeni kredi başvurusu yapar ve kredi ID'si döner.
KrediOdeme: Kredi borcundan ödeme yapar, kalan borcu günceller.
KrediDurumGuncelle: Kredi borcunu kontrol eder ve borç kapanmışsa durumu günceller.
*/

CREATE OR REPLACE PACKAGE Kredi_Pkg IS

    PROCEDURE KrediBasvuru(
        p_MusteriID NUMBER,
        p_AnaPara   NUMBER,
        p_FaizOrani NUMBER,
        p_Vade      DATE,
        o_KrediID   OUT NUMBER
    );

    PROCEDURE KrediOdeme(
        p_KrediID NUMBER, 
        p_Tutar   NUMBER
    );

    PROCEDURE KrediDurumGuncelle(
        p_KrediID NUMBER
    );

END Kredi_Pkg;

/*
KrediBasvuru: Yeni kredi başvurusu yapar,kayıt oluşturur,kalan borç ana para ile başlar ve kredi ID'si döner.
KrediOdeme: Kredi borcundan ödeme yapar,kalan borcu günceller. Ödeme miktarı kalan borçtan fazla olamaz.
KrediDurumGuncelle: Kredi borcunu kontrol eder ve borç kapanmışsa durumu günceller.
*/

CREATE OR REPLACE PACKAGE BODY Kredi_Pkg IS

    PROCEDURE KrediBasvuru(
        p_MusteriID NUMBER,
        p_AnaPara   NUMBER,
        p_FaizOrani NUMBER,
        p_Vade      DATE,
        o_KrediID   OUT NUMBER
    ) IS
    BEGIN
        INSERT INTO Kredi (MusteriID, AnaPara, KalanBorc, FaizOrani, VadeTarihi)
        VALUES (p_MusteriID, p_AnaPara, p_AnaPara, p_FaizOrani, p_Vade)
        RETURNING KrediID INTO o_KrediID;
    END KrediBasvuru;

    PROCEDURE KrediOdeme(
        p_KrediID NUMBER, 
        p_Tutar   NUMBER
    ) IS
        v_Kalan NUMBER;
    BEGIN
        -- Krediye ait kalan borcu sorgula
        SELECT KalanBorc 
        INTO v_Kalan 
        FROM Kredi 
        WHERE KrediID = p_KrediID;

        -- Ödeme miktarı kalan borçtan fazla olamaz
        IF v_Kalan < p_Tutar THEN
            RAISE_APPLICATION_ERROR(-20030, 'Ödenecek tutar kalan borçtan fazla olamaz.');
        END IF;

        -- Kalan borcu güncelle
        UPDATE Kredi 
        SET KalanBorc = KalanBorc - p_Tutar 
        WHERE KrediID = p_KrediID;

        -- Kredi durumunu kontrol et ve güncelle
        KrediDurumGuncelle(p_KrediID);
    END KrediOdeme;

    PROCEDURE KrediDurumGuncelle(
        p_KrediID NUMBER
    ) IS
        v_Kalan NUMBER;
    BEGIN
        -- Kalan borcu kontrol et
        SELECT KalanBorc 
        INTO v_Kalan 
        FROM Kredi 
        WHERE KrediID = p_KrediID;

        -- Borç tamamen kapandıysa durumu güncelle
        IF v_Kalan <= 0 THEN
            UPDATE Kredi 
            SET Durum = 'KAPANDI' 
            WHERE KrediID = p_KrediID;
        END IF;
    END KrediDurumGuncelle;

END Kredi_Pkg;

/* kodları çalıştırma*/

-- 10 000 müşterimiz vardı, bunun 1500 ü aktif değildi
-- Bu yüzden geri kalan 8500 müşteri için 8500 hesap açtık
-- Bu 8500 hesabın 500 ünü KAPALI hale getirmiştik.
-- Geri kalan 8000 açık hesap üzerinde işlemler yapmak istiyoruz
-- 6000 adet kredi işlemi yapmak istiyoruz.
-- Bu random olarak seçilmiş 6000 hesaba 
-- Random olarak kredi başvurusu, kredi ödeme, kredi durum güncelleme işlemleri uyguluyoruz

-- Önce 6000 müşteriye kredi başvurusu yaptık
DECLARE
    CURSOR c_AktifMusteriler IS
        SELECT m.MusteriID
        FROM Musteri m
        WHERE m.Durum = 'AKTIF'
          AND EXISTS (
                SELECT 1
                FROM Hesap h
                WHERE h.MusteriID = m.MusteriID
                  AND h.Durum = 'AÇIK'
          )
        ORDER BY DBMS_RANDOM.VALUE;  -- RANDOM SIRALAMA

    v_Sayac    NUMBER := 0;
    v_KrediID  NUMBER;
BEGIN
    DBMS_RANDOM.SEED(TO_NUMBER(TO_CHAR(SYSDATE, 'SSSSS')));

    FOR r IN c_AktifMusteriler LOOP
        v_Sayac := v_Sayac + 1;

        -- RANDOM seçilmiş ilk 6000 müşteri kredi başvurusu yapacak
        IF v_Sayac <= 6000 THEN

            Kredi_Pkg.KrediBasvuru(
                p_MusteriID => r.MusteriID,
                p_AnaPara   => TRUNC(DBMS_RANDOM.VALUE(5000, 200000)), -- 5k - 200k
                p_FaizOrani => TRUNC(DBMS_RANDOM.VALUE(10, 35)),         -- %10 - %35
                p_Vade      => SYSDATE + TRUNC(DBMS_RANDOM.VALUE(90, 720)), -- 3 ay - 2 yıl
                o_KrediID   => v_KrediID
            );

        ELSE
            -- Geri kalan aktif müşterilere kredi açmıyoruz
            NULL;
        END IF;

    END LOOP;

    COMMIT;
END;
/

-- Sonra kredilere random ödemeler yaptık, bazı borçlar sıfırlandı ve biten borçların kredi hesaplarını Kapalı duruma getirdik.

DECLARE
    CURSOR c_AktifKrediler IS
        SELECT KrediID, KalanBorc
        FROM Kredi
        WHERE Durum = 'AKTIF';

    v_OdemeTipi NUMBER;  -- 1 = ödeme yok, 2 = kısmi ödeme, 3 = tam ödeme
    v_OdemeTutar NUMBER;
BEGIN
    DBMS_RANDOM.SEED(TO_NUMBER(TO_CHAR(SYSDATE, 'SSSSS')));

    FOR r IN c_AktifKrediler LOOP

        -- Ödeme tipi:
        -- 1 = hiç ödeme yapma
        -- 2 = kısmi ödeme (KalanBorc'un %10-60 arası)
        -- 3 = tam ödeme (KalanBorc'un tamamı)
        v_OdemeTipi := TRUNC(DBMS_RANDOM.VALUE(1, 4));

        IF v_OdemeTipi = 1 THEN
            -- Bu krediye hiç ödeme yapılmayacak
            NULL;

        ELSIF v_OdemeTipi = 2 THEN
            -- Kısmi ödeme
            v_OdemeTutar := TRUNC(r.KalanBorc * DBMS_RANDOM.VALUE(0.10, 0.60));

            IF v_OdemeTutar > 0 THEN
                BEGIN
                    Kredi_Pkg.KrediOdeme(
                        p_KrediID => r.KrediID,
                        p_Tutar   => v_OdemeTutar
                    );
                EXCEPTION
                    WHEN OTHERS THEN
                        NULL; -- Her ihtimale karşı hata durumlarını yut
                END;
            END IF;

        ELSE
            -- Tam ödeme (Kalan borcun tamamını ödemeye çalış)
            IF r.KalanBorc > 0 THEN
                BEGIN
                    Kredi_Pkg.KrediOdeme(
                        p_KrediID => r.KrediID,
                        p_Tutar   => r.KalanBorc
                    );
                EXCEPTION
                    WHEN OTHERS THEN
                        NULL;
                END;
            END IF;

        END IF;

    END LOOP;

    COMMIT;
END;
/

SELECT * FROM Kredi;