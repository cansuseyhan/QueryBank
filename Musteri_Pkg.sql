/* 
MusteriID: Her müşteri için otomatik artan benzersiz kimlik.
Ad ve Soyad: Müşterinin adı ve soyadı, zorunlu alanlar.
TCNo: T.C. kimlik numarası, benzersiz ve zorunlu.
DogumTarihi: Müşterinin doğum tarihi.
RiskSkoru: Müşterinin kredi ve ödeme geçmişine bağlı olarak otomatik hesaplanan risk skoru.
SubeID: Müşterinin kayıtlı olduğu şube kodu.
Durum: Müşterinin aktif veya pasif olduğunu belirten durum alanı.
*/

CREATE TABLE Musteri (
    MusteriID   NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
    Ad          VARCHAR2(50) NOT NULL,
    Soyad       VARCHAR2(50) NOT NULL,
    TCNo        CHAR(11) UNIQUE NOT NULL,
    DogumTarihi DATE NOT NULL,
    RiskSkoru   NUMBER DEFAULT 0,
    SubeID      NUMBER NOT NULL,
    KayitTarihi DATE DEFAULT SYSDATE,
    Durum       VARCHAR2(20) DEFAULT 'AKTIF' -- AKTIF, PASIF
);

/*
RiskSkoruHesapla: Verilen müşteri için risk skorunu döndüren fonksiyon.
MusteriAc: Yeni müşteri kaydı oluşturan ve otomatik risk skoru hesaplayan prosedür.
MusteriPasifEt: Belirtilen müşteriyi pasif duruma getiren prosedür.
*/

CREATE OR REPLACE PACKAGE Musteri_Pkg IS

    FUNCTION RiskSkoruHesapla (
        p_MusteriID NUMBER
    ) RETURN NUMBER;

    PROCEDURE MusteriAc (
        p_Ad        VARCHAR2,
        p_Soyad     VARCHAR2,
        p_TCNo      CHAR,
        p_Dogum     DATE,
        p_SubeID    NUMBER,
        o_MusteriID OUT NUMBER
    );

    PROCEDURE MusteriPasifEt (
        p_MusteriID NUMBER
    );

END Musteri_Pkg;

/*
RiskSkoruHesapla fonksiyonu, müşterinin aktif kredi sayısı ve gecikmiş kredi sayısına göre basit bir puanlama yapıyor.
MusteriAc prosedürü, yeni müşteri kaydı eklerken aynı zamanda risk skorunu otomatik güncelliyor.
MusteriPasifEt ise müşteriyi sistemden aktif olmayan olarak işaretliyor.
*/

CREATE OR REPLACE PACKAGE BODY Musteri_Pkg IS

   FUNCTION RiskSkoruHesapla(p_MusteriID NUMBER) RETURN NUMBER IS
      v_Risk          NUMBER := 0;
      v_KrediSayisi   NUMBER;
      v_GecikmeSayisi NUMBER;
   BEGIN
      -- Müşterinin aktif kredi sayısını sayar
      SELECT COUNT(*) 
        INTO v_KrediSayisi 
        FROM Kredi 
       WHERE MusteriID = p_MusteriID 
         AND Durum = 'AKTIF';

      -- Vadesi geçmiş ve ödenmemiş kredi sayısını sayar
      SELECT COUNT(*) 
        INTO v_GecikmeSayisi 
        FROM Kredi 
       WHERE MusteriID = p_MusteriID 
         AND VadeTarihi < SYSDATE 
         AND KalanBorc > 0;

      -- Risk skoru hesaplama formülü
      v_Risk := v_KrediSayisi * 20 + v_GecikmeSayisi * 50;

      RETURN v_Risk;
   END RiskSkoruHesapla;

   PROCEDURE MusteriAc(
      p_Ad        VARCHAR2,
      p_Soyad     VARCHAR2,
      p_TCNo      CHAR,
      p_Dogum     DATE,
      p_SubeID    NUMBER,
      o_MusteriID OUT NUMBER
   ) IS
   BEGIN
      -- Yeni müşteri kaydı ekler
      INSERT INTO Musteri (Ad, Soyad, TCNo, DogumTarihi, SubeID)
      VALUES (p_Ad, p_Soyad, p_TCNo, p_Dogum, p_SubeID)
      RETURNING MusteriID INTO o_MusteriID;

      -- Risk skorunu hesaplar ve günceller
      UPDATE Musteri 
         SET RiskSkoru = RiskSkoruHesapla(o_MusteriID) 
       WHERE MusteriID = o_MusteriID;
   END MusteriAc;

   PROCEDURE MusteriPasifEt(p_MusteriID NUMBER) IS
   BEGIN
      -- Müşteriyi pasif duruma getirir
      UPDATE Musteri 
         SET Durum = 'PASIF' 
       WHERE MusteriID = p_MusteriID;
   END MusteriPasifEt;

END Musteri_Pkg;

/* kodları çalıştırma*/

-- tabloya 10 000 adet müşteri insert ediyoruz, MusteriAc Prosedürüyle
-- 50 adet Türkçe isim ve soyisim listeleri oluşturup içinden random olarak seçim yaptık
-- ilk hanesi 1 ve 9 arasında olan 11 haneli random TC kimlik numaraları ürettik
-- bugünün tarihinden random bir şekilde geriye giderek doğum tarihleri ürettik
-- ve mod alıp 1 ile 10 arasında şube id ler ürettik
DECLARE
   TYPE t_list IS TABLE OF VARCHAR2(50);
   
   v_isimler t_list := t_list(
      'Ahmet','Mehmet','Ayşe','Fatma','Ali','Veli','Can','Cansu','Deniz','Ece',
      'Burak','Emre','Selin','Zeynep','Hakan','Murat','Gökhan','Seda','Elif','Gamze',
      'Serkan','Okan','Yusuf','Halil','Osman','Melis','Buse','Pelin','Şule','Sevim',
      'Ferhat','Kadir','Onur','Barış','Tolga','İpek','Aslı','Nazlı','Hande','Mine',
      'Tuba','Sibel','Nil','Arda','Kerem','Levent','Özlem','Sevgi','Gül','Funda'
   );

   v_soyisimler t_list := t_list(
      'Yılmaz','Kaya','Demir','Çelik','Şahin','Öztürk','Aydın','Arslan','Doğan','Kılıç',
      'Aslan','Koç','Özdemir','Polat','Erdoğan','Yıldız','Aksoy','Güneş','Bozkurt','Avcı',
      'Taş','Kurt','Bulut','Özkan','Işık','Ay','Kara','Çetin','Uçar','Öz',
      'Ergin','Kaplan','Yalçın','Özbay','Tunç','Sağlam','Baran','Ergin','Toprak','Sezer',
      'Gür','Erden','Özbek','Altun','Deniz','Çakır','Eren','Er','Öztuna','Bal'
   );

   v_MusteriID NUMBER;
   v_Ad        VARCHAR2(50);
   v_Soyad     VARCHAR2(50);
   v_TCNo      CHAR(11);
BEGIN
   FOR i IN 1..10000 LOOP
      -- Rastgele isim ve soyisim seç
      v_Ad    := v_isimler(TRUNC(DBMS_RANDOM.VALUE(1, v_isimler.COUNT+1)));
      v_Soyad := v_soyisimler(TRUNC(DBMS_RANDOM.VALUE(1, v_soyisimler.COUNT+1)));

      -- 11 haneli TCNo üret (ilk hanesi 1–9 arası)
      v_TCNo := TO_CHAR(TRUNC(DBMS_RANDOM.VALUE(1,10))) ||
                LPAD(TRUNC(DBMS_RANDOM.VALUE(0,9999999999)),10,'0');

      -- Prosedürü çağırma  
      Musteri_Pkg.MusteriAc(
         p_Ad        => v_Ad,
         p_Soyad     => v_Soyad,
         p_TCNo      => v_TCNo,
         p_Dogum     => TRUNC(SYSDATE - DBMS_RANDOM.VALUE(7000, 20000)), -- rastgele doğum tarihi
         p_SubeID    => MOD(i, 10) + 1, -- 1-10 arası şube id
         o_MusteriID => v_MusteriID
      );
   END LOOP;
   
   COMMIT;
END;
/

-- bu 10 000 müşteriden random 1500 ünü Pasif hale getirelim
BEGIN    
   FOR r IN (
      SELECT MusteriID 
        FROM (
               SELECT MusteriID 
                 FROM Musteri 
                ORDER BY DBMS_RANDOM.VALUE
             ) 
       WHERE ROWNUM <= 1500
   ) LOOP
      Musteri_Pkg.MusteriPasifEt(r.MusteriID);
   END LOOP;
   
   COMMIT;
END;
/

-- şimdi bu 10000 müşterinin risk skorunu hesaplatıyoruz,update işlemi yeni satır eklemez.
-- Kredi tablosunun dolu olması lazım
BEGIN
   FOR r IN (SELECT MusteriID FROM Musteri) LOOP
      UPDATE Musteri
         SET RiskSkoru = Musteri_Pkg.RiskSkoruHesapla(r.MusteriID)
       WHERE MusteriID = r.MusteriID;
   END LOOP;
   
   COMMIT;
END;
/


SELECT * FROM Musteri; 
