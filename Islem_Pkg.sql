/*
IslemID: Her işleme ait benzersiz kimlik.
HesapNo: İşlemin ait olduğu hesap numarası.
IslemTipi: İşlem türü (örneğin YATIRMA, CEKME, TRANSFER_GONDEREN, TRANSFER_ALICI).
Tutar: İşlemde hareket eden tutar.
IslemTarihi: İşlem gerçekleşme zamanı, varsayılan olarak sistem tarihinden alınır.
Aciklama: İşlemle ilgili ek bilgi.
*/

CREATE TABLE Islem (
    IslemID     NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
    HesapNo     NUMBER NOT NULL,
    IslemTipi   VARCHAR2(20) NOT NULL,
    Tutar       NUMBER NOT NULL,
    IslemTarihi DATE DEFAULT SYSDATE,
    Aciklama    VARCHAR2(200),
    
    CONSTRAINT fk_islem_hesap
        FOREIGN KEY (HesapNo) REFERENCES Hesap(HesapNo)
);

/* 
ParaYatir: Belirtilen hesaba tutar kadar para yatırır ve işlem kaydını oluşturur.
ParaCek: Hesaptan belirtilen tutarda para çeker, bakiye kontrolü yapar ve işlem kaydını tutar.
TransferYap: Bir hesaptan diğerine tutar transferi yapar, bakiye kontrolü sağlar ve her iki hesap için işlem kaydı oluşturur.
*/

CREATE OR REPLACE PACKAGE Islem_Pkg IS

    PROCEDURE ParaYatir(
        p_HesapNo NUMBER,
        p_Tutar   NUMBER
    );

    PROCEDURE ParaCek(
        p_HesapNo NUMBER, 
        p_Tutar   NUMBER
    );

    PROCEDURE TransferYap(
        p_HesapNo      NUMBER, 
        p_HedefHesapNo NUMBER, 
        p_Tutar        NUMBER
    );

END Islem_Pkg;


/*
ParaYatir prosedürü, hesap bakiyesine belirtilen tutarı ekler ve işlem tablosuna yatırma kaydı oluşturur.
ParaCek prosedürü, bakiye kontrolü yapar, yeterli bakiye varsa bakiyeden tutarı düşer ve çekme işlemini kaydeder.
TransferYap prosedürü, gönderen hesapta bakiye kontrolü yapar, tutarı gönderen bakiyesinden düşer, alıcı bakiyesine ekler ve her iki hesaba işlem kaydı oluşturur.
*/

CREATE OR REPLACE PACKAGE BODY Islem_Pkg IS

    PROCEDURE ParaYatir(
        p_HesapNo NUMBER, 
        p_Tutar   NUMBER
    ) IS
    BEGIN
        -- Hesap bakiyesine yatırılan tutarı ekle
        Hesap_Pkg.BakiyeGuncelle(p_HesapNo, p_Tutar);
        
        -- İşlem tablosuna kayıt ekle
        INSERT INTO Islem (HesapNo, IslemTipi, Tutar, Aciklama)
        VALUES (p_HesapNo, 'YATIRMA', ROUND(p_Tutar, 2), 'Hesaba para yatırıldı');
    END ParaYatir;

    PROCEDURE ParaCek(
        p_HesapNo NUMBER, 
        p_Tutar   NUMBER
    ) IS
        v_Bakiye NUMBER;
    BEGIN
        -- Önce güncel bakiye sorgulanır
        v_Bakiye := Hesap_Pkg.BakiyeSorgula(p_HesapNo);

        -- Eğer bakiye yetersizse hata verilir
        IF v_Bakiye < p_Tutar THEN
            RAISE_APPLICATION_ERROR(-20020, 'Yetersiz bakiye.');
        END IF;

        -- Bakiyeden çekilen tutar çıkarılır
        Hesap_Pkg.BakiyeGuncelle(p_HesapNo, -p_Tutar);
        
        -- İşlem kaydı eklenir
        INSERT INTO Islem (HesapNo, IslemTipi, Tutar, Aciklama)
        VALUES (p_HesapNo, 'CEKME', ROUND(p_Tutar, 2), 'Hesaptan para çekildi');
    END ParaCek;

    PROCEDURE TransferYap(
        p_HesapNo      NUMBER, 
        p_HedefHesapNo NUMBER, 
        p_Tutar        NUMBER
    ) IS
        v_Bakiye NUMBER;
    BEGIN
        -- Gönderen hesabın bakiyesi kontrol edilir
        v_Bakiye := Hesap_Pkg.BakiyeSorgula(p_HesapNo);

        IF v_Bakiye < p_Tutar THEN
            RAISE_APPLICATION_ERROR(-20021, 'Yetersiz bakiye.');
        END IF;

        -- Gönderenin bakiyesinden düşülür
        Hesap_Pkg.BakiyeGuncelle(p_HesapNo, -p_Tutar);

        -- Alıcının bakiyesine eklenir
        Hesap_Pkg.BakiyeGuncelle(p_HedefHesapNo, p_Tutar);
        
        -- İşlem kayıtları ayrı ayrı eklenir
        INSERT INTO Islem (HesapNo, IslemTipi, Tutar, Aciklama)
        VALUES (p_HesapNo, 'TRANSFER_GONDEREN', ROUND(p_Tutar, 2), 'Transfer gönderildi');

        INSERT INTO Islem (HesapNo, IslemTipi, Tutar, Aciklama)
        VALUES (p_HedefHesapNo, 'TRANSFER_ALICI', ROUND(p_Tutar, 2), 'Transfer alındı');
    END TransferYap;

END Islem_Pkg;

/* kodları çalıştırma*/

-- 10 000 müşterimiz vardı, bunun 1500 ü aktif değildi
-- Bu yüzden geri kalan 8500 müşteri için 8500 hesap açtık
-- Bu 8500 hesabın 500 ünü KAPALI hale getirmiştik.
-- Geri kalan 8000 açık hesap üzerinde işlemler yapmak istiyoruz
-- Ancak 500 ü hiç işlem yapmasın sadece açık kalsın diyelim
-- Geriye 7500 adet işlem yapılacak hesabımız oldu.
-- Bu random olarak seçilmiş 7500 hesaba 
-- Random olarak para çekme, para yatırma , transfer işlemleri uyguluyoruz

DECLARE    
    CURSOR c_AcikHesaplar IS
        SELECT HesapNo
        FROM Hesap
        WHERE Durum = 'AÇIK'
        ORDER BY DBMS_RANDOM.VALUE;  -- RANDOM SIRALAMA

    v_IslemNo      NUMBER := 0;
    v_Tutar        NUMBER;
    v_IslemTipi    NUMBER;
    v_HedefHesapNo NUMBER;
BEGIN
    FOR r IN c_AcikHesaplar LOOP
        v_IslemNo := v_IslemNo + 1;

        -- RANDOM seçilmiş ilk 7500 hesap işlem alacak
        IF v_IslemNo <= 7500 THEN

            v_Tutar      := TRUNC(DBMS_RANDOM.VALUE(50, 500000));
            v_IslemTipi  := TRUNC(DBMS_RANDOM.VALUE(1, 4)); -- 1=Yatır, 2=Çek, 3=Transfer

            IF v_IslemTipi = 1 THEN
                -- PARA YATIR
                Islem_Pkg.ParaYatir(r.HesapNo, v_Tutar);
                
            ELSIF v_IslemTipi = 2 THEN
                -- PARA ÇEK
                BEGIN
                    Islem_Pkg.ParaCek(r.HesapNo, v_Tutar);
                EXCEPTION
                    WHEN OTHERS THEN
                        NULL; -- Yetersiz bakiye olabilir
                END;

            ELSE
                -- TRANSFER
                SELECT HesapNo
                INTO v_HedefHesapNo
                FROM (
                    SELECT HesapNo
                    FROM Hesap
                    WHERE Durum = 'AÇIK'
                      AND HesapNo <> r.HesapNo
                    ORDER BY DBMS_RANDOM.VALUE
                )
                WHERE ROWNUM = 1;

                BEGIN
                    Islem_Pkg.TransferYap(r.HesapNo, v_HedefHesapNo, v_Tutar);
                EXCEPTION
                    WHEN OTHERS THEN
                        NULL;
                END;

            END IF;

        ELSE
            -- RANDOM seçilmiş son 500 açık hesap işlem almayacak
            NULL;
        END IF;

    END LOOP;

    COMMIT;
END;
/



SELECT * FROM Islem